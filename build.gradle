// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'depends.gradle'


buildscript {
    ext {
        versions = [
                buildTool       : '3.0.0',
                multidex        : '1.0.1',
                retrolambda     : '3.7.0',
                sdk             : '26.1.0',
                constraintLayout: '1.0.2',
                butterKnife     : '8.8.1',
                guava           : '22.0-android',
                storeBox        : '1.4.0',
                DBFlow          : '4.1.1',
                kotlin          : '1.1.51',
                dagger          : '2.12',
                okhttp          : '3.9.0',
                jackson         : '2.9.2',
                rx              : '2.0.1',
                netty           : '4.1.16.Final',
        ]
    }

    repositories {
        jcenter()
        google()
//      maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // For Butter Knife SNAPSHOT
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.buildTool}"
//      classpath "me.tatarka:gradle-retrolambda:${versions.retrolambda}" // Plugin support by java 8, Enable if SDK version < 24
        classpath "com.jakewharton:butterknife-gradle-plugin:${versions.butterKnife}"
        // Butter Knife support
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        // Kotlin support

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
//  configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        google()

        maven { url 'https://www.jitpack.io' }
//      maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // For Butter Knife SNAPSHOT
    }
}

subprojects {
//  apply plugin: 'me.tatarka.retrolambda'  // Plugin support by java 8, Enable if SDK version < 24
//  apply plugin: 'com.jakewharton.butterknife'

    if (project.name.startsWith("lib.common")) {
        return
    }

    if (project.name.startsWith("lib.")) {
        apply plugin: 'com.android.library'
    } else {
        apply plugin: 'com.android.application'

        dependencies {
            implementation project(":lib.common")
            implementation project(":lib.resource")
            implementation project(":lib.widget")
        }
    }

    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])

        implementation libs.main.common
    }

    android {
        compileSdkVersion 27
        buildToolsVersion "27"

        defaultConfig {
            multiDexEnabled true

            minSdkVersion 26
            targetSdkVersion 27
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        packagingOptions {
            exclude 'LICENSE.txt'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
        }

        lintOptions {
            abortOnError false
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
